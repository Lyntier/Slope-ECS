import org.gradle.api.internal.tasks.testing.results.DefaultTestResult

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    // For publishing to Maven Central, we'll probably want these.
    id 'maven'
    id 'signing'
}

javadoc {
    source(sourceSets.main.allJava)
    failOnError(false)
    options.stylesheetFile(new File(projectDir, "src/main/javadoc/slope-custom.css"))
}

java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group('io.github.lucasstarsz')
archivesBaseName = 'Slope-ECS'
version('0.0.2')

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13'
}

tasks.withType(Test) {
    testLogging {

        def totalTestTime = 0

        afterTest { desc, DefaultTestResult result ->
            totalTestTime += result.endTime - result.startTime
        }

        afterSuite { desc, DefaultTestResult result ->
            if (!desc.parent) { // will match the outermost suite
                def passedFailedSkipped = "${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
                def output = "Test Suite Results: ${result.resultType} (${result.testCount} tests, ${passedFailedSkipped}) in ${totalTestTime} ms."
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength) + '\n')

                if (("${result.resultType}" != "SUCCESS")) {
                    System.exit(0)
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(
                        userName: System.getenv('ossrhUsername'),
                        password: System.getenv('ossrhPassword')
                )
            }

            pom.project {
                packaging 'jar'
                name = 'Slope Entity Component System'
                // optionally artifactId can be defined here
                description('A application used as an example on how to set up pushing its ' +
                        'components to the Central Repository.')
                url = 'https://github.com/lucasstarsz/Slope-ECS'

                scm {
                    connection = 'scm:git:https://github.com/lucasstarsz/Slope-ECS.git'
                    developerConnection = 'scm:git:https://github.com/lucasstarsz/Slope-ECS.git'
                    url = 'https://github.com/lucasstarsz/Slope-ECS.git'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/lucasstarsz/Slope-ECS/blob/main/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id = 'andrewd'
                        name = 'Andrew Dey'
                        email = 'manfred@sonatype.com'
                    }
                }
            }
        }
    }
}
